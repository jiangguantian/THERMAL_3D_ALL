!~~~~~~~~~~~定义1、2、3、5号钢构件截面~~~~~~~~~~~!
!-------------------START-----------------------!
FINISH
/CLEAR
/INPUT,Sections,mac
!--------------------END-------------------------!

!------------------------------------------------!
FINISH
/CLEAR
/FILNAME,BeiPanJiang_Archbridge
/TITLE,The Analysis of Stiff Skeleton
/PREP7
/NERR,,99999999
!~~~~~~~~~~~~~定义MAT,ETYPE及参数~~~~~~~~~~~~~~~~!
ET,1,BEAM189
ET,2,SHELL63
!~~~~~~~~~~~~~钢材
MP,EX,1,2.1E11
MP,PRXY,1,0.3
MP,DENS,1,7850
MP,ALPX,1,1E-5!!钢材的热膨胀系数
!~~~~~~~~~~~~~C40（墩）、C55（梁）钢筋混凝土
MP,EX,2,3.4E10
MP,PRXY,2,0.25
MP,DENS,2,2550
MP,ALPX,2,1E-5!!混凝土的热膨胀系数
!~~~~~~~~~~~~~C60钢筋混凝土
MP,EX,3,3.6E10
MP,PRXY,3,0.25
MP,DENS,3,2550
MP,ALPX,3,1E-5!!混凝土的热膨胀系数
R,1,0.5
R,2,0.55
R,3,0.6
R,4,0.65
R,5,0.7
R,6,0.75
R,7,0.8
R,8,0.9
R,9,1.1
!~~~~~~~~~~~~~C80钢筋混凝土
MP,EX,4,3.8E10
MP,PRXY,4,0.25
MP,DENS,4,2550
MP,ALPX,4,1E-5!!混凝土的热膨胀系数
!~~~~~~~~~~~~~杆件分段数
N=3
MA=6
!------------------------------------------------!

/INPUT,1-5KP1,mac
/INPUT,6-20KP1,mac
/INPUT,1-5KPB1,mac
/INPUT,6-7KPB1,mac
/INPUT,8-20KPB1,mac 
/INPUT,DZKP1,mac 
/INPUT,LXLKP,mac   




!~~~~~~~~~~~~~导入1-5号钢构件截面~~~~~~~~~~~~~~~~!
!-------------------START-----------------------!
!~~~~~~~~~~~~~~~~~~导入1号截面L90*90*12
SECTYPE,1,BEAM,MESH
SECOFFSET,USER
SECREAD,L90X90X12,SECT,,MESH
!SECCONTROLS,,,,63.76
!~~~~~~~~~~~~~~~~~~导入2号截面L90*90*10
SECTYPE,2,BEAM,MESH
SECOFFSET,USER
SECREAD,L90X90X10,SECT,,MESH
!SECCONTROLS,,,,53.904
!~~~~~~~~~~~~~~~~~~导入3号截面L110*110*14
SECTYPE,3,BEAM,MESH
SECOFFSET,USER
SECREAD,L110X110X14,SECT,,MESH
!SECCONTROLS,,,,91.236
!~~~~~~~~~~~~~~~~~~导入4号截面空心钢管(偏心)
!SECTYPE,4,BEAM,MESH
!SECOFFSET,USER,,-0.1
!SECREAD,GGHNT,SECT,,MESH
!SECCONTROLS,,,,429.7+987
!~~~~~~~~~~~~~~~~~~被注释掉的这一段是钢管混凝土联合截面的建立
!~~~~~~~~~~~~~~~~~~现在采用分开建立的方式，因此不用上面的这段
SECTYPE,4,BEAM,CTUBE
SECDATA,0.351,0.375
SECOFFSET,USER,,-0.1
!~~~~~~~~~~~~~~~~~~导入99号截面钢管内混凝土截面(偏心)
SECTYPE,99,BEAM,CSOLID
SECDATA,0.351
SECOFFSET,USER,,-0.1
!~~~~~~~~~~~~~~~~~~导入5号截面L90*90*12
SECTYPE,5,BEAM,MESH
SECOFFSET,USER
SECREAD,N4,SECT,,MESH
!SECCONTROLS,,,,31.88
!--------------------END-----------------------!

!----------------------------------------------------!
!~~~~~~~~~~~~~~~~~~定义交界墩截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义6号截面（交界墩1-1截面）
SECTYPE,6,BEAM,RECT
SECDATA,7.5,8.37
!~~~~~~~~~~~~~~~~~~定义7号截面（交界墩2-2截面）
SECTYPE,7,BEAM,RECT
SECDATA,7.5,7.72
!~~~~~~~~~~~~~~~~~~定义8号截面（交界墩2'-2'截面）
SECTYPE,8,BEAM,HREC
SECDATA,7.5,7.72,1.5,1.5,0.9,0.9
!~~~~~~~~~~~~~~~~~~定义9号截面（交界墩3-3截面）
SECTYPE,9,BEAM,HREC
SECDATA,7.5,7.01,1.5,1.5,0.9,0.9
!~~~~~~~~~~~~~~~~~~定义10号截面（交界墩4-4截面）
SECTYPE,10,BEAM,HREC
SECDATA,7.5,5.66,1.5,1.5,0.9,0.9
!~~~~~~~~~~~~~~~~~~定义11号截面（交界墩4'-4'截面）
SECTYPE,11,BEAM,HREC
SECDATA,7.5,5.6,1.5,1.5,0.7,0.7
!~~~~~~~~~~~~~~~~~~定义12号截面（交界墩5-5截面）
SECTYPE,12,BEAM,HREC
SECDATA,7.5,5.01,1.5,1.5,0.7,0.7
!~~~~~~~~~~~~~~~~~~定义13号截面（交界墩6-6截面）
SECTYPE,13,BEAM,HREC
SECDATA,7.5,4.50,1.5,1.5,0.7,0.7
!~~~~~~~~~~~~~~~~~~定义14号截面（交界墩7-7截面）(7-7截面偏心为上部)
SECTYPE,14,BEAM,RECT
SECDATA,4.50,5.0
SECOFFSET,USER,,2.5
!~~~~~~~~~~~~~~~~~~定义15号截面（交界墩8-8截面）
SECTYPE,15,BEAM,HREC
SECDATA,4.50,4.0,0.8,0.8,0.8,0.8
!-------------------------16#~20#变截面-----------------------------!
!~~~~~~~~~~~~~~~~~~定义16号变截面（交界墩1-2截面）
SECTYPE,16,TAPER
SECDATA,6,-11.4,8.98,4.6
SECDATA,7,-11.4,8.51,16.6
!~~~~~~~~~~~~~~~~~~定义17号变截面（交界墩2'-3截面）
SECTYPE,17,TAPER
SECDATA,8,-11.4,8.51,16.6
SECDATA,9,-11.4,7.99,29.6
!~~~~~~~~~~~~~~~~~~定义18号变截面（交界墩3-4截面）
SECTYPE,18,TAPER
SECDATA,9,-11.4,7.99,29.6
SECDATA,10,-11.4,7.00,54.6
!~~~~~~~~~~~~~~~~~~定义19号变截面（交界墩4'-5截面）
SECTYPE,19,TAPER
SECDATA,11,-11.4,7.00,54.6
SECDATA,12,-11.4,6.27,79.6
!~~~~~~~~~~~~~~~~~~定义20号变截面（交界墩5-6截面）
SECTYPE,20,TAPER
SECDATA,12,-11.4,6.27,79.6
SECDATA,13,-11.4,5.58,106.6
!-------------------------21#~25#变截面-----------------------------!
!~~~~~~~~~~~~~~~~~~定义21号变截面（交界墩1-2截面）
SECTYPE,21,TAPER
SECDATA,6,-11.4,-8.98,4.6
SECDATA,7,-11.4,-8.51,16.6
!~~~~~~~~~~~~~~~~~~定义22号变截面（交界墩2'-3截面）
SECTYPE,22,TAPER
SECDATA,8,-11.4,-8.51,16.6
SECDATA,9,-11.4,-7.99,29.6
!~~~~~~~~~~~~~~~~~~定义23号变截面（交界墩3-4截面）
SECTYPE,23,TAPER
SECDATA,9,-11.4,-7.99,29.6
SECDATA,10,-11.4,-7.00,54.6
!~~~~~~~~~~~~~~~~~~定义24号变截面（交界墩4'-5截面）
SECTYPE,24,TAPER
SECDATA,11,-11.4,-7.00,54.6
SECDATA,12,-11.4,-6.27,79.6
!~~~~~~~~~~~~~~~~~~定义25号变截面（交界墩5-6截面）
SECTYPE,25,TAPER
SECDATA,12,-11.4,-6.27,79.6
SECDATA,13,-11.4,-5.58,106.6
!-------------------------END-----------------------------!


!----------------------------------------------------!
!~~~~~~~~~~~~~~~~~~定义D1~D5(1#墩到5#墩截面)截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义26号截面（1#墩1-1截面）
SECTYPE,26,BEAM,MESH
SECOFFSET,USER
SECREAD,D1_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义27号截面（1#墩2-2截面）(2-2截面偏心为上部)
SECTYPE,27,BEAM,RECT
SECDATA,4,3
SECOFFSET,USER,0,1.5
!~~~~~~~~~~~~~~~~~~定义28号截面（1#墩3-3截面）
SECTYPE,28,BEAM,MESH
SECOFFSET,USER
SECREAD,D1_3-3,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义29号截面（2#墩1-1截面）
SECTYPE,29,BEAM,MESH
SECOFFSET,USER
SECREAD,D2_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义30号截面（2#墩2-2截面）(2-2截面偏心为上部)
SECTYPE,30,BEAM,RECT
SECDATA,3.5,3
SECOFFSET,USER,0,1.5
!~~~~~~~~~~~~~~~~~~定义31号截面（2#墩3-3截面）
SECTYPE,31,BEAM,MESH
SECOFFSET,USER
SECREAD,D2_3-3,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义32号截面（3#墩1-1截面）
SECTYPE,32,BEAM,MESH
SECOFFSET,USER
SECREAD,D3_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义33号截面（4#、5#墩1-1截面）(偏心为中心)
SECTYPE,33,BEAM,RECT
SECDATA,3.5,3
SECOFFSET,USER
!-------------------------END-----------------------------!


!----------------------------------------------------!
!~~~~~~~~~~~~~~~~~~定义LXL(连续梁截面)等截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义34号截面（连续梁1-1截面）
SECTYPE,34,BEAM,MESH
SECOFFSET,USER
SECREAD,LXL_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义35号截面（连续梁2-2截面）
SECTYPE,35,BEAM,MESH
SECOFFSET,USER
SECREAD,LXL_2-2,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义36号截面（连续梁3-3截面）
SECTYPE,36,BEAM,MESH
SECOFFSET,USER
SECREAD,LXL_3-3,SECT,,MESH
!-------------------------END-----------------------------!



!~~~~~~~~~~~~~~~~~~定义LXL(连续梁截面)变截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!---------------定义小里程侧变截面37#~40#
!~~~~~~~~~~~~~~~~~~定义37号变截面（连续梁1-2截面）
J=23000
SECTYPE,37,TAPER
SECDATA,34,KX(J+2),KY(J+2),KZ(J+2)
SECDATA,35,KX(J+3),KY(J+3),KZ(J+3)!对应连续梁的2、3号点
!~~~~~~~~~~~~~~~~~~定义38号变截面（连续梁2-3截面）
SECTYPE,38,TAPER
SECDATA,35,KX(J+3),KY(J+3),KZ(J+3)
SECDATA,36,KX(J+4),KY(J+4),KZ(J+4)!对应连续梁的3、4号点
!~~~~~~~~~~~~~~~~~~定义39号变截面（连续梁3-2截面）
SECTYPE,39,TAPER
SECDATA,36,KX(J+6),KY(J+6),KZ(J+6)
SECDATA,35,KX(J+7),KY(J+7),KZ(J+7)!对应连续梁的6、7号点
!~~~~~~~~~~~~~~~~~~定义40号变截面（连续梁2-1截面）
SECTYPE,40,TAPER
SECDATA,35,KX(J+7),KY(J+7),KZ(J+7)
SECDATA,34,KX(J+8),KY(J+8),KZ(J+8)!对应连续梁的7、8号点

!---------------定义大里程侧变截面41#~44#
!~~~~~~~~~~~~~~~~~~定义41号变截面（连续梁1-2截面）
SECTYPE,41,TAPER
SECDATA,34,KX(J+18),KY(J+18),KZ(J+18)
SECDATA,35,KX(J+19),KY(J+19),KZ(J+19)!对应连续梁的18、19号点
!~~~~~~~~~~~~~~~~~~定义42号变截面（连续梁2-3截面）
SECTYPE,42,TAPER
SECDATA,35,KX(J+19),KY(J+19),KZ(J+19)
SECDATA,36,KX(J+20),KY(J+20),KZ(J+20)!对应连续梁的19、20号点
!~~~~~~~~~~~~~~~~~~定义43号变截面（连续梁3-2截面）
SECTYPE,43,TAPER
SECDATA,36,KX(J+22),KY(J+22),KZ(J+22)
SECDATA,35,KX(J+23),KY(J+23),KZ(J+23)!!对应连续梁的22、23号点
!~~~~~~~~~~~~~~~~~~定义44号变截面（连续梁2-1截面）
SECTYPE,44,TAPER
SECDATA,35,KX(J+23),KY(J+23),KZ(J+23)
SECDATA,34,KX(J+24),KY(J+24),KZ(J+24)!对应连续梁的23、24号点
!-------------------------END-----------------------------!




!----------------------------START-----------------------------!
!~~~~~~~~~~~~~建立小里程左侧四分之一模型~~~~~~~~~~~~~~~~~~~~!
!~~~~~~~~~~~~~建立骨架1-5#和骨架6-20#和合拢段骨架~~~~~~~~~~~~~~~~~~~~!
/INPUT,GJ1-5-1,MAC
/INPUT,GJ6-20-1,MAC
/INPUT,HLD1,MAC
!~~~~~~~~~~~~~~~~~~建立1/4外包混凝土~~~~~~~~~~~~~~~~~~~~!
/INPUT,HNTB1-5-1,MAC
/INPUT,HNTB6-7-1,MAC
/INPUT,HNTB8-20-1,MAC
/INPUT,HLDB1,MAC
!~~~~~~~~~~~~~~~~~~建立交界墩和D1~D5~~~~~~~~~~~~~~~~~~~~!
/INPUT,DZ1,MAC
!----------------------------END-----------------------------!



!~~~~~~~~~~~建立上部结构连续梁形成全桥结构~~~~~~~~~~~~~!
/INPUT,LXL,MAC

!~~~~~~~~~~~划分所有线单元
LSEL,S,TYPE,,1
LMESH,ALL
!~~~~~~~~~~~划分所有面单元
ASEL,S,TYPE,,2
AMESH,ALL
!~~~~~~~~~~~对称四分之一实体结构形成全桥结构~~~~~~~~~~~~~!
/INPUT,SYMM,MAC
CSDELE,ALL
!~~~~~~~~~~~融合所有重合节点
ALLSEL
NUMMRG,NODE




!~~~~~~~~~~~~~--------求解模块-----------~~~~~~~~~~~~~~~~~~~~!
!-------------------------START-----------------------------!
FINI
/SOLU
ALLSEL,ALL
TREF,15!假设合拢时温度为15度
BF,ALL,TEMP,30!假设升温15度
!ACEL,,,9.8

!----------------------START-----------------------!
!----------------定义主拱圈的约束-----------------!
KWPLAN,1,1002,1001,1003
CSYS,4
NSEL,S,LOC,Z,-0.01,0.01
NSEL,R,LOC,X,-0.1,28!后面发现有些交界墩的点也被约束故加上这个选择
NSEL,R,LOC,Y,-0.1,9!后面发现有些交界墩的点也被约束故加上这个选择
D,ALL,ALL
CSYS,0

A=-KX(1002)+445
B=KY(1002)
C=KZ(1002)
D=KP(A,B,C)

A=-KX(1001)+445
B=KY(1001)
C=KZ(1001)
E=KP(A,B,C)

A=-KX(1003)+445
B=KY(1003)
C=KZ(1003)
F=KP(A,B,C)

KWPLAN,1,D,E,F
CSYS,4
NSEL,S,LOC,Z,-0.01,0.01
NSEL,R,LOC,X,-0.1,28!后面发现有些交界墩的点也被约束故加上这个选择
NSEL,R,LOC,Y,-0.1,9!后面发现有些交界墩的点也被约束故加上这个选择
D,ALL,ALL
CSYS,0
!----------------------END-----------------------!

!----------------------START-----------------------!
!----------------定义交界墩的约束-------------------!
NSEL,S,LOC,X,-11.4
NSEL,R,LOC,Z,4.6
D,ALL,ALL
NSEL,S,LOC,X,11.4+445
NSEL,R,LOC,Z,4.6
D,ALL,ALL
ALLSEL,ALL
!----------------------END-----------------------!


!----------------------START-----------------------!
!!!定义D1的墩底(D1B)和拱圈之间的约束,D1墩底对应骨架图中27号点（第7节段）D1B_1_1代表1#墩_墩底节点_四分之一!!!!
!!!D1B_2_1代表1#墩_墩底节点的从属节点_四分之一!!!!
D1B_1_1=KP(54.5,4.9,50.59)
D1B_2_1=7000+5
D1B_3_1=7000+6
D1B_4_1=7000+26
D1B_5_1=7000+9
D1B_6_1=7000+8
D1B_7_1=7000+29


*DO,I,1,7
X=KX(D1B_%I%_1)
Y=KY(D1B_%I%_1)
Z=KZ(D1B_%I%_1)
D1B_%I%_2=KP(X,-Y,Z)
D1B_%I%_3=KP(445-X,Y,Z)
D1B_%I%_4=KP(445-X,-Y,Z)
*ENDDO


*DO,I,1,4
KSEL,S,,,D1B_1_%I%
KSEL,A,,,D1B_2_%I%
KSEL,A,,,D1B_3_%I%
KSEL,A,,,D1B_4_%I%
KSEL,A,,,D1B_5_%I%
KSEL,A,,,D1B_6_%I%
KSEL,A,,,D1B_7_%I%
NSLK,S
CP,NEXT,ALL,ALL
*ENDDO
ALLSEL,ALL
!----------------------END-----------------------!

!----------------------START-----------------------!
!!!定义D2的墩底(D2B)和拱圈之间的约束,D2墩底对应骨架图中43号点（第11节段）D2B_1_1代表2#墩_墩底节点_四分之一!!!!
!!!D2B_2_1代表2#墩_墩底节点的从属节点_四分之一!!!!
D2B_1_1=KP(96.5,4.9,74.83)
D2B_2_1=11000+5
D2B_3_1=11000+6
D2B_4_1=11000+26
D2B_5_1=11000+9
D2B_6_1=11000+8
D2B_7_1=11000+29


*DO,I,1,7
X=KX(D2B_%I%_1)
Y=KY(D2B_%I%_1)
Z=KZ(D2B_%I%_1)
D2B_%I%_2=KP(X,-Y,Z)
D2B_%I%_3=KP(445-X,Y,Z)
D2B_%I%_4=KP(445-X,-Y,Z)
*ENDDO


*DO,I,1,4
KSEL,S,,,D2B_1_%I%
KSEL,A,,,D2B_2_%I%
KSEL,A,,,D2B_3_%I%
KSEL,A,,,D2B_4_%I%
KSEL,A,,,D2B_5_%I%
KSEL,A,,,D2B_6_%I%
KSEL,A,,,D2B_7_%I%
NSLK,S
CP,NEXT,ALL,ALL
*ENDDO
ALLSEL,ALL
!----------------------END-----------------------!

!----------------------START-----------------------!
!!!定义D3的墩底(D3B)和拱圈之间的约束,D3墩底对应骨架图中57号点（第14节段）D3B_1_1代表3#墩_墩底节点_四分之一!!!!
!!!D3B_2_1代表3#墩_墩底节点的从属节点_四分之一!!!!
D3B_1_1=KP(138.5,4.9,91.52)
D3B_2_1=14000+15
D3B_3_1=14000+16
D3B_4_1=14000+35
D3B_5_1=14000+19
D3B_6_1=14000+18
D3B_7_1=14000+38


*DO,I,1,7
X=KX(D3B_%I%_1)
Y=KY(D3B_%I%_1)
Z=KZ(D3B_%I%_1)
D3B_%I%_2=KP(X,-Y,Z)
D3B_%I%_3=KP(445-X,Y,Z)
D3B_%I%_4=KP(445-X,-Y,Z)
*ENDDO


*DO,I,1,4
KSEL,S,,,D3B_1_%I%
KSEL,A,,,D3B_2_%I%
KSEL,A,,,D3B_3_%I%
KSEL,A,,,D3B_4_%I%
KSEL,A,,,D3B_5_%I%
KSEL,A,,,D3B_6_%I%
KSEL,A,,,D3B_7_%I%
NSLK,S
CP,NEXT,ALL,ALL
*ENDDO
ALLSEL,ALL
!----------------------END-----------------------!

!----------------------START-----------------------!
!!!定义D4的墩底(D4B)和拱圈之间的约束,D4墩底对应骨架图中70号点（第18节段）D4B_1_1代表4#墩_墩底节点_四分之一!!!!
!!!D4B_2_1代表4#墩_墩底节点的从属节点_四分之一!!!!
D4B_1_1=KP(180.5,4.9,101.28)
D4B_2_1=18000+1
D4B_3_1=18000+2
D4B_4_1=18000+4
D4B_5_1=18000+3



*DO,I,1,5
X=KX(D4B_%I%_1)
Y=KY(D4B_%I%_1)
Z=KZ(D4B_%I%_1)
D4B_%I%_2=KP(X,-Y,Z)
D4B_%I%_3=KP(445-X,Y,Z)
D4B_%I%_4=KP(445-X,-Y,Z)
*ENDDO


*DO,I,1,4
KSEL,S,,,D4B_1_%I%
KSEL,A,,,D4B_2_%I%
KSEL,A,,,D4B_3_%I%
KSEL,A,,,D4B_4_%I%
KSEL,A,,,D4B_5_%I%
NSLK,S
CP,NEXT,ALL,ALL
*ENDDO
ALLSEL,ALL!!!必须有，因为后面的KP()命令指的是在所选择的点集当中离这个坐标最近的点号
!----------------------END-----------------------!

!----------------------START-----------------------!
!!!定义D5的墩底(D5B)和拱圈之间的约束,D5墩底对应骨架图中83号点（HLD）D5B_1_1代表5#墩_墩底节点_四分之一!!!!
!!!D5B_2_1代表5#墩_墩底节点的从属节点_四分之一!!!!
D5B_1_1=KP(222.5,4.9,104.5)
D5B_1_2=KP(222.5,-4.9,104.5)
D5B_2_1=20000+21
D5B_3_1=20000+22
D5B_4_1=20000+24
D5B_5_1=20000+23



*DO,I,2,5
X=KX(D5B_%I%_1)
Y=KY(D5B_%I%_1)
Z=KZ(D5B_%I%_1)
D5B_%I%_2=KP(X,-Y,Z)
D5B_%I%_3=KP(445-X,Y,Z)
D5B_%I%_4=KP(445-X,-Y,Z)
*ENDDO


*DO,I,1,2
KSEL,S,,,D5B_1_%I%
KSEL,A,,,D5B_2_%I%
KSEL,A,,,D5B_3_%I%
KSEL,A,,,D5B_4_%I%
KSEL,A,,,D5B_5_%I%

KSEL,A,,,D5B_2_%I+2%
KSEL,A,,,D5B_3_%I+2%
KSEL,A,,,D5B_4_%I+2%
KSEL,A,,,D5B_5_%I+2%
NSLK,S
CP,NEXT,ALL,ALL
*ENDDO
ALLSEL,ALL
!----------------------END-----------------------!

!----------------------START-----------------------!
!!!定义两边T构最外边的约束!!!!
J=23000
KSEL,S,,,J+1
KSEL,A,,,J+25
NSLK,S
D,ALL,UY,,,,,UZ,ROTX,ROTZ
!!!定义两边T构中心与墩柱的自由度耦合!!!!
J=22000!代表JJD，+1000代表LXL
KSEL,S,,,J+13
KSEL,A,,,J+1000+5
NSLK,S
CP,NEXT,ALL,ALL
ALLSEL,ALL
X=KX(J+13)
Y=KY(J+13)
Z=KZ(J+13)
A=KP(445-X,Y,Z)
KSEL,S,,,A
KSEL,A,,,J+1000+21
NSLK,S
CP,NEXT,ALL,ALL
ALLSEL,ALL
!!!定义T构中间连续梁和相应墩柱的自由度耦合!!!!
J=23000!代表LXL
*DO,I,9,17
X=KX(J+I)
Y=KY(J+I)
Z=109.3
A=KP(X,Y,Z)
KSEL,S,,,A
KSEL,A,,,J+I
NSLK,S
CP,NEXT,UY,ALL
CP,NEXT,UZ,ALL
CP,NEXT,ROTX,ALL
CP,NEXT,ROTZ,ALL
ALLSEL,ALL
*ENDDO
ALLSEL,ALL
!----------------------END-----------------------!
ALLSEL,ALL
ANTYPE,0
SOLVE
FINISH
!---------------------------END-----------------------------!






!!~~~~~~~~~~~~~~~~~~后处理模块~~~~~~~~~~~~~~~~~~~~!
!--------------------START-----------------------!
/POST1

!---------------------------------------!
!~~~~以下命令用于求钢管的最大应力~~~~~~~~~!
!~~~~找轴向应力+弯曲应力的最大值~~~~~~~~!
!---------------------------------------!
LSEL,NONE
*DO,I,1,20
LSEL,A,,,WS1%I%
LSEL,A,,,WX1%I%
LSEL,A,,,NS1%I%
LSEL,A,,,NX1%I%
*ENDDO
ESLL

!-----钢管I端最大应力
ETABLE,SDIRI,SMISC,31
ETABLE,SBZTI,SMISC,34
ETABLE,SBZBI,SMISC,35
SADD,SIGI_1,SDIRI,SBZTI
SADD,SIGI_2,SDIRI,SBZBI
PLETAB,SIGI_1
/WAIT,10
PLETAB,SIGI_2
/WAIT,10
PRETAB,SIGI_1,SIGI_2


!!-----钢管J端最大应力（先不用）
!ETABLE,SDIRJ,SMISC,36
!ETABLE,SBZTJ,SMISC,39
!ETABLE,SBZBJ,SMISC,40
!SADD,SIGJ_1,SDIRJ,SBZTJ
!SADD,SIGJ_2,SDIRJ,SBZBJ
!PLETAB,SIGJ_1,SIGJ_2
!PRETAB,SIGJ_1,SIGJ_2
!---------------------END------------------------!



!---------------------------------------!
!~~~~以下命令用于求管内混凝土的最大应力~~~~~~~~~!
!~~~~找轴向应力+弯曲应力的最大值~~~~~~~~!
!---------------------------------------!
LSEL,NONE
*DO,I,1,20
LSEL,A,,,WSHNT1%I%
LSEL,A,,,WXHNT1%I%
LSEL,A,,,NSHNT1%I%
LSEL,A,,,NXHNT1%I%
*ENDDO
ESLL
!-----钢管内混凝土I端最大应力
ETABLE,SDIRI,SMISC,31
ETABLE,SBZTI,SMISC,34
ETABLE,SBZBI,SMISC,35
SADD,SIGI_1,SDIRI,SBZTI
SADD,SIGI_2,SDIRI,SBZBI
PLETAB,SIGI_1
/WAIT,10
PLETAB,SIGI_2
/WAIT,10
PRETAB,SIGI_1,SIGI_2


!!-----钢管内混凝土J端最大应力（先不用）
!ETABLE,SDIRJ,SMISC,36
!ETABLE,SBZTJ,SMISC,39
!ETABLE,SBZBJ,SMISC,40
!SADD,SIGJ_1,SDIRJ,SBZTJ
!SADD,SIGJ_2,SDIRJ,SBZBJ
!PLETAB,SIGJ_1,SIGJ_2
!PRETAB,SIGJ_1,SIGJ_2
!---------------------END------------------------!




!---------------------------------------!
!~~~~以下命令用于求混凝土板的最大压应力~~~~~~~~~!
!---------------------------------------!
ESEL,S,TYPE,,2
ETABLE,S3D1,NMISC,3
ETABLE,S3D2,NMISC,8
ETABLE,S3D3,NMISC,13
ETABLE,S3D4,NMISC,18
PLETAB,S3D1
/WAIT,10
PLETAB,S3D2
/WAIT,10
PLETAB,S3D3
/WAIT,10
PLETAB,S3D4
/WAIT,10
PRETAB,S3D1,S3D2,S3D3,S3D4



